plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id 'checkstyle'
	id 'com.diffplug.spotless' version '6.25.0'
	id "de.undercouch.download" version "5.3.0"
    id 'maven-publish'
}

group = 'com.ingsis'
version = '0.0.1-SNAPSHOT'
description = 'Snippet Service for Ingsis'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/austral-ingsis/*")
        credentials {
            username = findProperty("gpr.user")
            password = findProperty("gpr.key")
        }
    }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	//implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	runtimeOnly 'org.postgresql:postgresql'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.azure:azure-storage-blob:12.27.0'
	implementation 'com.azure:azure-identity:1.12.2'

	implementation 'org.austral.ingsis:redis-streams-mvc:0.1.13'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	//testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'com.h2database:h2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy tasks.jacocoTestReport
}

tasks.register("installGitHooks") {
    doLast {
        if (System.getProperty("os.name").toLowerCase().contains("win")) {
            exec {
                commandLine "bash", "./scripts/install-hooks.sh"
            }
        } else {
            exec {
                commandLine "sh", "./scripts/install-hooks.sh"
            }
        }
    }
}
tasks.named("build") {
    dependsOn("installGitHooks")
}

task downloadNewrelic(type: Download) {
	mkdir 'newrelic'
	src 'https://download.newrelic.com/newrelic/java-agent/newrelic-agent/current/newrelic-java.zip'
	dest file('newrelic/newrelic-java.zip')
}

task unzipNewrelic(type: Copy) {
	from zipTree(file('newrelic/newrelic-java.zip'))
	into file('newrelic/extracted')
}

jacoco {
	toolVersion = "0.8.10"
}

jacocoTestReport {
	dependsOn tasks.test
	reports {
		xml.required = true
		html.required = true
	}
}

// FunciÃ³n para leer archivo .env y retornar un Map con las variables
def loadEnvFile() {
	def envVars = [:]
	def envFile = file('.env')
	if (envFile.exists()) {
		envFile.readLines().each { line ->
			line = line.trim()
			if (line && !line.startsWith('#') && line.contains('=')) {
				def parts = line.split('=', 2)
				if (parts.length == 2) {
					def key = parts[0].trim()
					def value = parts[1].trim()
					if (key && value) {
						envVars[key] = value
					}
				}
			}
		}
	}
	return envVars
}

// Configurar bootRun para usar variables de entorno del .env
bootRun {
	environment = loadEnvFile()
	// Configurar directorio temporal para evitar problemas de permisos en Windows
	def tempDir = System.getenv('TEMP') ?: System.getenv('TMP') ?: System.getProperty('java.io.tmpdir') ?: "${System.getProperty('user.home')}/AppData/Local/Temp"
	systemProperty 'java.io.tmpdir', tempDir
	
	// New Relic solo si el archivo existe
	def newrelicJar = file("${projectDir}/newrelic/extracted/newrelic.jar")
	if (newrelicJar.exists()) {
		jvmArgs = [
			"-javaagent:${newrelicJar.absolutePath}"
		]
	}
}

spotless {
    java {
        target 'src/**/*.java'
        eclipse().configFile("config/eclipse-java-style.xml")
        importOrder()
        removeUnusedImports()
        lineEndings = 'UNIX'
    }
}
checkstyle {
    toolVersion = '10.12.0'
    configFile = file("$rootDir/config/checkstyle/checkstyle.xml")
}