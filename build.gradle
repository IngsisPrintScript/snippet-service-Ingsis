plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id 'checkstyle'
	id 'com.diffplug.spotless' version '6.25.0'
	id 'de.undercouch.download' version '5.3.0'
	id 'maven-publish'
}

group = 'com.ingsis'
version = '0.0.1-SNAPSHOT'
description = 'Snippet Service for Ingsis'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
	maven {
		name = "GitHubPackages"
		url = uri("https://maven.pkg.github.com/austral-ingsis/*")
		credentials {
			username = findProperty("gpr.user")
			password = findProperty("gpr.key")
		}
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.austral.ingsis:redis-streams-mvc:0.1.13'

	runtimeOnly 'org.postgresql:postgresql'

	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.azure:azure-storage-blob:12.27.0'
	implementation 'com.azure:azure-identity:1.12.2'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.h2database:h2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

/* =======================
   New Relic
   ======================= */

tasks.register("downloadNewRelic", Download) {
	src "https://download.newrelic.com/newrelic/java-agent/newrelic-agent/current/newrelic-java.zip"
	dest file("$buildDir/newrelic/newrelic-java.zip")
}

tasks.register("unzipNewRelic", Copy) {
	dependsOn downloadNewRelic
	from zipTree("$buildDir/newrelic/newrelic-java.zip")
	into "$buildDir/newrelic"
}

tasks.named("bootJar") {
    dependsOn unzipNewRelic
}

/* =======================
   BootRun con Agent
   ======================= */

bootRun {
	dependsOn unzipNewRelic
	jvmArgs = [
			"-javaagent:${buildDir}/newrelic/newrelic.jar"
	]
}

/* =======================
   Testing / Coverage
   ======================= */

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy tasks.jacocoTestReport
}

jacoco {
	toolVersion = "0.8.10"
}

jacocoTestReport {
	dependsOn tasks.test
	reports {
		xml.required = true
		html.required = true
	}
}

/* =======================
   Hooks / Quality
   ======================= */

tasks.register("installGitHooks") {
	doLast {
		exec {
			commandLine(
					System.getProperty("os.name").toLowerCase().contains("win")
							? "bash"
							: "sh",
					"./scripts/install-hooks.sh"
			)
		}
	}
}

tasks.named("build") {
	dependsOn("installGitHooks")
}

spotless {
	java {
		target 'src/**/*.java'
		eclipse().configFile("config/eclipse-java-style.xml")
		importOrder()
		removeUnusedImports()
		lineEndings = 'UNIX'
	}
}

checkstyle {
	toolVersion = '10.12.0'
	configFile = file("$rootDir/config/checkstyle/checkstyle.xml")
}